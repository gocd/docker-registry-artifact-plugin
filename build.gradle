/*
 * Copyright 2022 Thoughtworks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'
apply from: "https://raw.githubusercontent.com/gocd/gocd-plugin-gradle-task-helpers/master/helper.gradle?_=${(int) (new Date().toInstant().epochSecond / 60)}"

gocdPlugin {
    id = 'cd.go.artifact.docker.registry'
    pluginVersion = '2.0.0'
    goCdVersion = '22.1.0'
    name = 'Artifact plugin for docker'
    description = 'Plugin allows to push/pull docker image from public or private docker registry'
    vendorName = 'Thoughtworks, Inc.'
    vendorUrl = 'https://github.com/gocd/docker-registry-artifact-plugin'

    githubRepo {
        owner = System.getenv('GITHUB_USER') ?: 'bob'
        repo = 'docker-registry-artifact-plugin'
        token = System.getenv('GITHUB_TOKEN') ?: 'bad-token'
    }

    pluginProject = project

    prerelease = !"No".equalsIgnoreCase(System.getenv('PRERELEASE'))
    assetsToRelease = [project.tasks.findByName('jar')]
}

version = gocdPlugin.fullVersion(project)

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

tasks.withType(JavaCompile).configureEach {
    options.release = 17
}

ext {
    deps = [
        gocdPluginApi: 'cd.go.plugin:go-plugin-api:25.3.0',
        awsSdk       : 'com.amazonaws:aws-java-sdk:1.12.791',
        slf4j        : 'org.slf4j:slf4j-api:2.0.17',
    ]

    versions = project.ext.deps.collectEntries { lib, libGav -> [lib, libGav.split(':').last()] }
}

dependencies {
    compileOnly project.deps.gocdPluginApi
    implementation 'com.google.code.gson:gson:2.13.2'
    implementation 'com.spotify:docker-client:8.16.0'
    implementation "com.amazonaws:aws-java-sdk-ecr:${project.versions.awsSdk}"
    implementation "com.amazonaws:aws-java-sdk-sts:${project.versions.awsSdk}"

    // Control/upgrade Spotify docker-client transitive dependencies
    implementation platform('com.fasterxml.jackson:jackson-bom:2.20.0')
    implementation platform('org.glassfish.jersey:jersey-bom:2.25')
    constraints {
        implementation('com.google.guava:guava:33.5.0-jre') {
            because "Spotify docker-client uses an older version"
        }
        implementation(project.deps.slf4j) {
            because "Spotify docker-client uses an older version"
        }
        implementation('org.apache.commons:commons-compress:1.28.0') {
            because "Spotify docker-client uses an older version"
        }
        implementation('commons-io:commons-io:2.20.0') {
            because "Spotify docker-client uses an older version"
        }
    }

    components {
        // workaround for Guava metadata declaring dependencies that are not needed at runtime
        // see https://github.com/google/guava/pull/6606
        withModule('com.google.guava:guava', { details ->
            details.allVariants {
                withDependencies {
                    removeAll {
                        it.name in [ "listenablefuture", "jspecify", "error_prone_annotations", "j2objc-annotations" ]
                    }
                }
            }
        })
    }

    modules {
        module('commons-logging:commons-logging') {
            replacedBy('org.slf4j:jcl-over-slf4j', "Everything should go via SLF4J")
        }
        module('org.bouncycastle:bcpkix-jdk15on') {
            replacedBy('org.bouncycastle:bcpkix-jdk18on', "Everything can go via the JDK 1.8+ BouncyCastle version")
        }
    }
    implementation('org.bouncycastle:bcpkix-jdk18on:1.82')
    implementation "org.slf4j:jcl-over-slf4j:${project.versions.slf4j}"

    testImplementation platform('org.junit:junit-bom:6.0.0')
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'org.junit.jupiter:junit-jupiter-params'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testImplementation project.deps.gocdPluginApi
    testImplementation 'uk.org.webcompere:system-stubs-jupiter:2.1.8'
    testImplementation 'org.assertj:assertj-core:3.27.6'
    testImplementation platform('org.mockito:mockito-bom:5.20.0')
    testImplementation 'org.mockito:mockito-core'
    testImplementation 'org.skyscreamer:jsonassert:1.5.3'
    testImplementation 'org.jsoup:jsoup:1.21.2'
}

test {
    useJUnitPlatform()
}

jar {
    from(configurations.runtimeClasspath) {
        into "lib/"
    }
}
